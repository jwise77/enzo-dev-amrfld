#include "fortran.def"
#include "phys_const.def"
!=======================================================================
!
! Copyright 2009 Daniel R. Reynolds
!
! This software is released under the terms of the "Enzo Public License"
! in the accompanying LICENSE file.
!
!=======================================================================
subroutine gFLDSplit_RadiationSource(Ersrc, time, a, ProbType, ESpectrum, &
     NGammaDot, EtaRadius, EtaCenter, aUnits, LenUnits, TimeUnits,        &
     ErUnits, Nx, Ny, Nz, NGxl, NGxr, NGyl, NGyr, NGzl, NGzr, x0L, x0R,   &
     x1L, x1R, x2L, x2R, ier)
!=======================================================================
!  written by: Daniel R. Reynolds
!  date:       July 2009
!
!  PURPOSE: Computes the emissivity point sources into the radiation 
!           energy equation
!
!  INPUTS:
!     time       - simulation time for evaluation
!     a          - cosmological expansion parameter
!     ProbType   - flag denoting physical problem to run
!     Nchem      - number of chemical species
!     HFrac      - percentage of mass composed of Hydrogen
!     ESpectrum  - radiation spectrum choice
!                       1 -> 1e5 black body spectrum
!                       0 -> power law spectrum
!                      -1 -> monochromatic 
!     NGammaDot  - ionization source
!     EtaRadius  - ionization source radius in cells
!     EtaCenter  - ionization source center (comoving, 3D coordinates in cm)
!     *Units     - variable scaling constants
!     Nx,Ny,Nz   - active mesh size in each direction
!     NG*l/NG*r  - left/right ghost cells in each direction
!     x*L/x*R    - left/right subdomain boundaries (comoving, no ghosts)
!
!     Note: the vector inputs are of size (Nx + NGxl + NGxr) in 
!     the x-direction, others are similar.
!
!  OUTPUT ARGUMENTS: 
!     Ersrc      - array of emissivity sources
!     ier        - success/failure flag (1->success, 0->failure)
!
!  EXTERNALS: 
!
!  LOCALS:
!
!=======================================================================
  implicit none

  !--------------
  ! argument declarations
  integer, intent(in) :: ESpectrum, ProbType
  integer, intent(in) :: Nx, NGxl, NGxr
  integer, intent(in) :: Ny, NGyl, NGyr
  integer, intent(in) :: Nz, NGzl, NGzr
  integer, intent(out) :: ier
  REALSUB, intent(in) :: a
  real,    intent(in) :: time, NGammaDot, EtaRadius
  real,    intent(in) :: EtaCenter(3)
  real,    intent(in) :: aUnits, LenUnits, TimeUnits, ErUnits
  real,    intent(in) :: x0L, x0R, x1L, x1R, x2L, x2R
  real,    intent(out) :: Ersrc(1-NGxl:Nx+NGxr,1-NGyl:Ny+NGyr,1-NGzl:Nz+NGzr)
  
  !--------------
  ! locals
  integer :: i, j, k
  real :: pi, h_nu0, etaconst, specconst, factor, etaloc(3)
  real :: dx, dy, dz, dV, cellXl, cellXr, cellYl, cellYr, cellZl, cellZr
  real :: cellXc, cellYc, cellZc, ECenter(3), ERadius, NGDot

!=======================================================================

!!$  write(*,*) 'Entering gFLDSplit::RadiationSource routine'
!!$  write(*,*) '   NGammaDot = ',NGammaDot
!!$  write(*,*) '   EtaRadius = ',EtaRadius
!!$  write(*,*) '   EtaCenter = ',EtaCenter

  ! initialize output to have all zero values, flag to success
  Ersrc = 0.d0
  ier = 1
  
  ! initialize constants
  pi    = pi_val
  dx    = (x0R-x0L)/Nx                ! mesh spacing (comoving), x0 direction
  dy    = (x1R-x1L)/Ny                ! mesh spacing (comoving), x1 direction
  dz    = (x2R-x2L)/Nz                ! mesh spacing (comoving), x2 direction
  dV    = dx*dy*dz*(LenUnits)**3      ! cell volume (proper)
  h_nu0 = 13.6d0*ev2erg               ! ionization energy of HI [ergs]

  ! scaling factor for T=10^5 blackbody spectrum
  if (ESpectrum == 1) then
     specconst = 1.52877652583602d0
  else
     specconst = 1.d0
  endif

  ! compute emissivity for various problems

  !   point-source emissivity at location (EtaCenter(1:3))
  if ((ProbType == 410) .or. (ProbType == 411) .or. &
      (ProbType == 413) .or. (ProbType == 415)) then

     ! one-cell source
     if (EtaRadius == 0.d0) then
        
        ! compute eta factor for given ionization source
        etaconst = h_nu0*NGammaDot*specconst/dV
        
        ! place ionization source in one cell
        do k=1,Nz,1
           
           ! z-boundaries (comoving) for this cell
           cellZl = x2L + (k-1)*dz
           cellZr = cellZl + dz
           
           do j=1,Ny,1
              
              ! y-boundaries (comoving) for this cell
              cellYl = x1L + (j-1)*dy
              cellYr = cellYl + dy
              
              do i=1,Nx,1
                 
                 ! x-boundaries (comoving) for this cell
                 cellXl = x0L + (i-1)*dx
                 cellXr = cellXl + dx
                 
                 ! see if domain center is in cell (or on left edge)
                 if ( (cellXl <= EtaCenter(1)) .and. (cellXr > EtaCenter(1)) .and. &
                      (cellYl <= EtaCenter(2)) .and. (cellYr > EtaCenter(2)) .and. &
                      (cellZl <= EtaCenter(3)) .and. (cellZr > EtaCenter(3)) ) then
                    Ersrc(i,j,k) = etaconst
                 endif
                 
              enddo
           enddo
        enddo

     else

        ! compute eta factor for given ionization source
        etaconst = h_nu0*NGammaDot*specconst/dV/8.d0/(EtaRadius**3)
        
        ! place ionization source in center of domain
        do k=1,Nz,1
           
           ! z-center (comoving) for this cell
           cellZc = x2L + (k-0.5d0)*dz
           
           do j=1,Ny,1
              
              ! y-center (comoving) for this cell
              cellYc = x1L + (j-0.5d0)*dy
              
              do i=1,Nx,1
                 
                 ! x-center (comoving) for this cell
                 cellXc = x0L + (i-0.5d0)*dx
                 
                 ! see if cell is within source region
                 if ( (abs(cellXc-EtaCenter(1)) < EtaRadius*dx) .and. &
                      (abs(cellYc-EtaCenter(2)) < EtaRadius*dy) .and. &
                      (abs(cellZc-EtaCenter(3)) < EtaRadius*dz) ) then
                    Ersrc(i,j,k) = etaconst
                 endif
                 
              enddo
           enddo
        enddo

     endif ! EtaRadius == 0

  !   emissivity flux along x=0 wall (NGammaDot photons/s/cm^2)
  else if (ProbType == 412) then

!!$     ! place ionization source along left wall (if on this subdomain)
!!$     if (x0L == 0.d0) then
!!$
!!$        ! compute eta factor for given ionization source, and put on wall
!!$!        etaconst = 1.0d6*h_nu0*specconst/dx/LenUnits
!!$        etaconst = 5.0d5*h_nu0*specconst/dx/LenUnits
!!$        do k=1,Nz,1
!!$           do j=1,Ny,1
!!$              Ersrc(1,j,k) = etaconst
!!$           enddo
!!$        enddo
!!$     endif

     ! place source of radius 1 at center of x-left face
     ECenter(1) = 0.d0
     ECenter(2) = 3.3d0
     ECenter(3) = 3.3d0
     ERadius = 1.d0
     NGDot = 3.d51

     ! compute eta factor for given ionization source
     etaconst = h_nu0*NGDot*specconst/dV/8.d0/(ERadius**3)
        
     ! place ionization source in center of domain
     do k=1,Nz,1

        ! z-center (comoving) for this cell
        cellZc = x2L + (k-0.5d0)*dz

        do j=1,Ny,1

           ! y-center (comoving) for this cell
           cellYc = x1L + (j-0.5d0)*dy

           do i=1,Nx,1

              ! x-center (comoving) for this cell
              cellXc = x0L + (i-0.5d0)*dx

              ! see if cell is within source region
              if ( (abs(cellXc-ECenter(1)) < ERadius*dx) .and. &
                   (abs(cellYc-ECenter(2)) < ERadius*dy) .and. &
                   (abs(cellZc-ECenter(3)) < ERadius*dz) ) then
                 Ersrc(i,j,k) = etaconst
              endif

           enddo
        enddo
     enddo
     
  !   point-source emissivity at center of every processor
  elseif (ProbType == 414) then

     ! compute eta factor for given ionization source
     etaconst = h_nu0*NGammaDot*specconst/dV
        
     ! place ionization source in center of subdomain
     Ersrc(int(Nx/2),int(Ny/2),int(Nz/2)) = etaconst
     
  !   homogeneous emissivity field w/ strength hnu0*NGammaDot/dV
  elseif (ProbType == 416) then

     ! place ionization source in center of subdomain
     Ersrc = h_nu0*NGammaDot*specconst/dV
     
  !   Iliev et al., test #4 (multiple sources in a cosmological medium)
  elseif (ProbType == 417) then

     ! place sources based on grid indices 
     factor = 1.d52*h_nu0*specconst*4.d0*pi/dV
     Ersrc(69,87,88)   = 0.646477039572334*factor
     Ersrc(68,120,103) = 0.687331910809231*factor
     Ersrc(61,79,65)   = 0.720977691827869*factor
     Ersrc(78,98,119)  = 0.745010302555466*factor
     Ersrc(74,97,123)  = 0.783462353719616*factor
     Ersrc(100,45,60)  = 0.869979626338959*factor
     Ersrc(86,10,27)   = 0.915642027721405*factor
     Ersrc(31,77,48)   = 0.939674638449001*factor
     Ersrc(104,55,62)  = 1.21845279688911*factor
     Ersrc(41,73,47)   = 1.63902316962204*factor
     Ersrc(73,89,96)   = 1.99710825046320*factor
     Ersrc(65,110,91)  = 2.27348358883057*factor
     Ersrc(77,91,106)  = 2.38643629225025*factor
     Ersrc(113,61,64)  = 3.25881936866198*factor
     Ersrc(124,62,61)  = 5.81348456600542*factor
     Ersrc(81,97,114)  = 7.96921044127083*factor

  !   Consolidated HII region with two sources
  elseif (ProbType == 418) then

     ! compute eta factor for given ionization source (8 cells touch)
     etaconst = h_nu0*NGammaDot*specconst/dV/8.d0

     ! place first ionization source
     etaloc(1) = 0.4d0  ! code units
     etaloc(2) = 0.d0
     etaloc(3) = 0.d0
     do k=1,Nz
        cellZc = x2L + (k-0.5d0)*dz
        do j=1,Ny
           cellYc = x1L + (j-0.5d0)*dy
           do i=1,Nx,1
              cellXc = x0L + (i-0.5d0)*dx
              if ( (abs(cellXc-etaloc(1)) < EtaRadius*dx) .and. &
                   (abs(cellYc-etaloc(2)) < EtaRadius*dy) .and. &
                   (abs(cellZc-etaloc(3)) < EtaRadius*dz) ) then
                 Ersrc(i,j,k) = etaconst
              endif
           enddo
        enddo
     enddo
        
     ! place second ionization source
     etaloc(1) = -0.4d0 
     etaloc(2) = 0.d0
     etaloc(3) = 0.d0
     do k=1,Nz
        cellZc = x2L + (k-0.5d0)*dz
        do j=1,Ny
           cellYc = x1L + (j-0.5d0)*dy
           do i=1,Nx,1
              cellXc = x0L + (i-0.5d0)*dx
              if ( (abs(cellXc-etaloc(1)) < EtaRadius*dx) .and. &
                   (abs(cellYc-etaloc(2)) < EtaRadius*dy) .and. &
                   (abs(cellZc-etaloc(3)) < EtaRadius*dz) ) then
                 Ersrc(i,j,k) = etaconst
              endif
           enddo
        enddo
     enddo

  endif ! ProbType

!!$  write(*,*) 'RadiationSource: individual source is ',etaconst
!!$  etaconst = sum(Ersrc)
!!$  write(*,*) 'RadiationSource: integrated source is ',etaconst
  
  return
end subroutine gFLDSplit_RadiationSource
!=======================================================================
